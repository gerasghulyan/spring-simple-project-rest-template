buildscript {
    ext {
        platformGroup = 'com.project.sample'
        platformVersion = '0.0.1'
        kotlinVersion = '1.3.61'
        vavrVersion = '0.10.2'
        easymockVersion = '4.0.2'
        apacheCommonsValidatorVersion = '1.4.1'
        testContainersVersion = '1.15.3'
        orikaSpringBootVersion = "1.8.0"
        springFoxSwaggerVersion = '2.9.2'
        owaspEncoderVersion = '1.2'
        swaggerAnnotationsVersion = '1.6.0'
    }

    repositories {
        mavenCentral()
        jcenter()
//        maven {
//            url 'https://nexus.project.domain.com/repository/maven-public'
//            credentials {
//                username System.env.SONATYPE_USERNAME
//                password System.env.SONATYPE_PASSWORD
//            }
//        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE' apply false
    id "jacoco"
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"
    id "org.ajoberstar.grgit" version "3.0.0"
    id 'com.bmuschko.docker-remote-api' version "6.7.0"
    id 'com.palantir.git-version' version '0.11.0'
    id "org.sonarqube" version "2.7.1"
}

def environmentPlatformVersion() {
    switch (grgit.getBranch().current().name) {
        case "develop":
            return "$platformVersion-SNAPSHOT"
        case "release":
            return "$platformVersion-release-SNAPSHOT"
        case "master":
            return platformVersion
        default:
            return "$platformVersion-SNAPSHOT"
    }
}

def versionPostfix() {
    switch (grgit.getBranch().current().name) {
        case "develop":
            return "-SNAPSHOT"
        case "release":
            return "-release-SNAPSHOT"
        case "master":
            return ""
        default:
            return "-SNAPSHOT"
    }
}

task printEnvVersion() {
    doLast {
        println version
    }
}

task tagRelease() {
    description = 'Tags the current head with the project\'s version.'
    doLast {
        grgit.tag.add {
            name = version
            message = "Release of ${version}"
        }
        grgit.push()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "io.spring.dependency-management"

    group = platformGroup
    version = environmentPlatformVersion()
    sourceCompatibility = '11'

    task makePretty(type: Delete) {
        delete 'out'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = sourceCompatibility
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = sourceCompatibility
    }

    repositories {
        mavenCentral()
        jcenter()
//        maven {
//            url 'https://nexus.project.domain.com/repository/maven-public'
//            credentials {
//                username System.env.SONATYPE_USERNAME
//                password System.env.SONATYPE_PASSWORD
//            }
//        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "http://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        implementation 'org.apache.commons:commons-lang3'
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "io.vavr:vavr"
//        implementation project(":commons:utils")
        testImplementation "org.easymock:easymock"
        testImplementation "junit:junit"
        testImplementation "org.assertj:assertj-core"
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.2.1.RELEASE") {
                bomProperty("kotlin.version", kotlinVersion)
            }
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
        }
        dependencies {
            dependency("org.easymock:easymock:$easymockVersion")
            dependency("org.easymock:easymock:$easymockVersion")
            dependency("io.vavr:vavr:$vavrVersion")
            dependency("commons-validator:commons-validator:$apacheCommonsValidatorVersion")
            dependency("org.testcontainers:postgresql:$testContainersVersion")
            dependency("net.rakugakibox.spring.boot:orika-spring-boot-starter:${orikaSpringBootVersion}")
            dependency("io.springfox:springfox-swagger2:$springFoxSwaggerVersion")
            dependency("io.springfox:springfox-swagger-ui:$springFoxSwaggerVersion")
            dependency("org.owasp.encoder:encoder:$owaspEncoderVersion")
            dependency("io.swagger:swagger-annotations:$swaggerAnnotationsVersion")
        }
    }

    jacoco {
        toolVersion = '0.8.5'
    }

}

project(":domain") {

    sonarqube.skipProject = true

    dependencies {
        api project(':commons:domain:persistence-domain')
    }
}

project(":persistence:persistence-core") {

    sonarqube.skipProject = true

    dependencies {
        implementation project(":domain")
    }
}

project(":persistence:persistence-impl") {

    sonarqube.skipProject = true

    dependencies {
        implementation project(":domain")
        implementation project(":persistence:persistence-core")
    }
}

project(":commons:persistence-utils") {

    sonarqube.skipProject = true

    group = "${platformGroup}.persistence"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }

}

project(":migration:migration-library") {

    sonarqube.skipProject = true

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.flywaydb:flyway-core'
        runtimeOnly 'org.postgresql:postgresql'
    }

}

project(":migration:migration-executable") {

    sonarqube.skipProject = true

    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "${platformGroup}.boot.MigrationBootApplication"
        manifest {
            attributes(
                    "Implementation-Version": environmentPlatformVersion()
            )
        }
    }
    dependencies {
        implementation project(":migration:migration-library")
        implementation "org.springframework.boot:spring-boot-starter"
    }
}

project(":helper:test-helper") {

    sonarqube.skipProject = true

    dependencies {
        implementation project(":domain")
        implementation project(":service:service-core")
        implementation project(":api:rest:rest-model")
        implementation "org.assertj:assertj-core"
        implementation 'org.springframework:spring-context'
    }
}

project(":helper:rest-test-helper") {

    apply plugin: 'maven'
    apply plugin: "kotlin-spring"

    sonarqube.skipProject = true

//    archivesBaseName = 'rest-test-helper'
//
//    uploadArchives {
//        repositories {
//            mavenDeployer {
//
////                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
////                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
////                }
////
////                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
////                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
////                }
//
//                pom.version = environmentPlatformVersion()
//                pom.artifactId = "rest-test-helper"
//                pom.groupId = platformGroup
//            }
//        }
//    }

    dependencies {
        api project(":commons:helper:commons-rest-test-helper")
        implementation project(":api:rest:rest-model")
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework:spring-context'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        api "org.assertj:assertj-core"
    }
}

project(":service:service-core") {

    sonarqube.skipProject = true

    group = "${platformGroup}.service"

    dependencies {
        implementation project(":domain")
        api project(":commons:service")
    }
}

project(":service:service-impl") {

    apply plugin: "kotlin-spring"
    apply plugin: "jacoco"

    group = "${platformGroup}.service"

    dependencies {
        implementation project(":domain")
        implementation project(":persistence:persistence-core")
        implementation project(":persistence:persistence-impl")
        implementation project(":commons:persistence-utils")
        implementation project(":service:service-core")
        implementation "org.springframework.security:spring-security-crypto"
        testImplementation project(":helper::test-helper")
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.easymock:easymock"
    }
}

project(":service:service-integration-test") {

    group = "${platformGroup}.service"
    apply plugin: "jacoco"
    apply plugin: "kotlin-spring"
    sonarqube.skipProject = true

    dependencies {
        testImplementation project(":helper:test-helper")
        testImplementation project(":domain")
        testImplementation project(":service:service-core")
        implementation project(":migration:migration-library")
        runtimeOnly project(":service:service-impl")
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.springframework.security:spring-security-crypto"
        testImplementation "org.testcontainers:postgresql"
        testImplementation "org.postgresql:postgresql"
    }
}

project(":api:rest:rest-model") {

    apply plugin: 'maven'
    sonarqube.skipProject = true

    archivesBaseName = 'rest-model-core'

    uploadArchives {
        repositories {
            mavenDeployer {

//                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
//                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
//                }
//
//                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
//                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
//                }

                pom.version = environmentPlatformVersion()
                pom.artifactId = "rest-model-core"
                pom.groupId = platformGroup
            }
        }
    }

    dependencies {
        api "com.fasterxml.jackson.core:jackson-annotations"
        api project(":commons:api-models")
        implementation "commons-validator:commons-validator"
    }
}

project(":api:rest:rest-client") {

    apply plugin: 'maven'
    sonarqube.skipProject = true

    archivesBaseName = 'rest-client-core'

    uploadArchives {
        repositories {
            mavenDeployer {

//                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
//                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
//                }
//
//                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
//                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
//                }

                pom.version = environmentPlatformVersion()
                pom.artifactId = "rest-client-core"
                pom.groupId = platformGroup
            }
        }
    }

    dependencies {
        compile project(":api:rest:rest-model")
        compile "org.springframework.cloud:spring-cloud-starter-openfeign"
    }
}

project(":api:rest:rest-facade") {

    apply plugin: "jacoco"
    group = "${platformGroup}.rest.facade"

    dependencies {
        implementation project(":api:rest:rest-model")
        implementation project(":service:service-core")
        implementation project(":commons:persistence-utils")
        runtimeOnly project(":service:service-impl")
        implementation project(":domain")
        implementation "net.rakugakibox.spring.boot:orika-spring-boot-starter"
        implementation "org.springframework:spring-context"
        testImplementation project(":helper:test-helper")
        testImplementation project(":helper:rest-test-helper")
        testImplementation "org.easymock:easymock"
    }
}

project(":api:rest:rest-resource") {

    group = "${platformGroup}.rest.resource"
    sonarqube.skipProject = true

    dependencies {
        api project(":api:rest:rest-facade")
        api project(":api:rest:rest-model")
        api "org.springframework.boot:spring-boot-starter-web"
        api "org.aspectj:aspectjrt"
        api "org.aspectj:aspectjweaver"
        api project(":commons:utils:web-utils")
    }
}

project(":api:rest:rest-resource-executable") {

    group = "${platformGroup}.rest.resource"

    sonarqube.skipProject = true
    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "${platformGroup}.rest.resource.boot.SpringBootWebApplication"
        manifest {
            attributes(
                    "Implementation-Version": environmentPlatformVersion()
            )
        }
    }

    dependencies {
        implementation project(":api:rest:rest-resource")
        implementation project(":migration:migration-library")
        implementation 'org.springframework.data:spring-data-jpa'
        implementation "io.springfox:springfox-swagger2"
        implementation "io.springfox:springfox-swagger-ui"
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
        implementation "org.postgresql:postgresql"
        implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider"
    }
}

project(":commons:api-models") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "api-models"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        api "com.fasterxml.jackson.core:jackson-annotations"
        api 'org.apache.commons:commons-lang3'
        api "io.swagger:swagger-annotations"
    }
}

project(":commons:helper:commons-rest-test-helper") {

    apply plugin: 'maven'

    archivesBaseName = 'commons-rest-test-helper'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "commons-rest-test-helper"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        api project(":commons:api-models")
        api "org.assertj:assertj-core"
    }
}

project(":commons:domain:persistence-domain") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "persistence-domain"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        dependencies {
            api project(":commons:domain:persistence-commons")
            api 'org.springframework.boot:spring-boot-starter-data-jpa'
        }
    }
}

project(":commons:domain:persistence-commons") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "persistence-commons"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        dependencies {
            api "org.apache.commons:commons-lang3"
        }
    }
}

project(":commons:service") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "service"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        dependencies {
            api project(":commons:domain:persistence-domain")
        }
    }
}

project(":commons:persistence") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "persistence"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        dependencies {
            implementation('org.springframework:spring-core') { transitive = false }
        }
    }
}

project(":commons:utils") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.groupId = platformGroup
                pom.artifactId = "commons-utils"
                pom.version = environmentPlatformVersion()
            }
        }
    }

    dependencies {
        api "org.springframework.data:spring-data-commons"
        api "org.owasp.encoder:encoder"
        testImplementation "org.easymock:easymock"
        testImplementation 'org.springframework:spring-test'
        testImplementation 'org.springframework.boot:spring-boot-test'
        testImplementation "org.aspectj:aspectjrt"
        testImplementation "org.aspectj:aspectjweaver"
    }

}

project(":commons:utils:web-utils") {

    apply plugin: 'maven'

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "https://nexus.project.domain.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.project.domain.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }
            }
        }
    }

    dependencies {
        api "io.vavr:vavr"
        api project(":commons:api-models")
        api 'org.springframework:spring-web'
    }
}

//region Jacoco unit tests
configure(subprojects.findAll {
    it.name == "service-impl" || it.name == "rest-facade"
}) {
    jacocoTestReport {
        reports {
            csv.enabled = false
            xml.enabled = true
            html.enabled = true
            html.destination file("${buildDir}/reports/coverage")
            println("jacoco coverage report: ${project.name}")
            println("file://${html.destination}/index.html")
        }
        afterEvaluate {
            classDirectories.from = files(classDirectories.files.collect {
                fileTree(
                        dir: it,
                        includes: [
                                'com/project/sample/service/**',
                                'com/project/sample/service/**',
                                'com/project/sample/rest/facade/**',
                        ],
                        exclude: [
                                'com/project/sample/service/conf/**',
                                'com/project/sample/rest/facade/conf/**'
                        ])
            })
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'CLASS'
                includes = ['com.project.sample.service.*.impl.*',
                            'com.project.sample.rest.facade.*.impl.*'
                ]
                excludes = [
                        '*Exception',
                ]
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.0
                }
                failOnViolation true
            }
        }
    }

    build.dependsOn jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
}
//endregion

//region Jacoco integration tests
configure(subprojects.findAll {
    it.name == "service-integration-test"
}) {
    jacocoTestReport {

        reports {
            csv.enabled = false
            xml.enabled = true
            html.enabled = true
            html.destination file("${buildDir}/reports/coverage")
            println("jacoco coverage report: ${project.name}")
            println("file://${html.destination}/index.html")
        }
        afterEvaluate {
            if (project.name == "service-integration-test") {
                classDirectories.from = files(files(rootProject.project(":service:service-impl").buildDir).collect().collect {
                    fileTree(
                            dir: it,
                            includes: [
                                    'classes/java/main/com/project/sample/service/**'
                            ],
                            exclude: [
                            ])
                })
                additionalSourceDirs.from("${rootProject.project(":service:service-impl").projectDir}/src/main/java")
            }
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                List allFiles = files(rootProject.project(":api:rest:rest-facade").buildDir).collect()
                allFiles.addAll(files(rootProject.project(":service:service-impl").buildDir).collect())
                classDirectories.from = files(allFiles.collect {
                    fileTree(
                            dir: it,
                            includes: [
                                    'classes/java/main/com/project/sample/rest/facade/**',
                                    'classes/java/main/com/project/sample/service/**'
                            ],
                            exclude: [
                            ])
                })
                element = 'CLASS'
                includes = ['com.project.sample.service.*.impl.*',
                            'com.project.sample.rest.facade.*.impl.*'
                ]
                excludes = [
                        '*Exception'
                ]
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.0
                }
                failOnViolation false
            }
        }
    }

    build.dependsOn jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
}
//endregion

sonarqube {

    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.projectKey", "YOUR_PROJECT_KEY"
        property "sonar.organization", "YOUR_ORGANIZATION"
        //property "sonar.login", "YOUR_LOGIN"
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

configure(subprojects.findAll {
    it.name == "rest-resource-executable" || it.name == "scheduler" || it.name == "migration-executable"
}) {

    apply plugin: 'com.bmuschko.docker-remote-api'

    def registry = project.findProperty("dockerRegistry") ?: ""
    def JAVA_VERSION = project.findProperty("javaVersion") ?: "11"

    if (registry) {
        registry += "/"
    }

    task createDockerfile(type: Dockerfile) {
        destFile = project.layout.buildDirectory.file('docker/Dockerfile')
        
        from "us.gcr.io/YOUR_PLATFORM_NAME/ubi8-openjdk-${JAVA_VERSION}-runtime"
        user("root")
        runCommand("mkdir -p /data")
        runCommand("chown -R jboss:jboss /data")
        runCommand("chmod -R 750 /data")
        user("jboss")

        label(
                [
                        'maintainer': 'admin@email.email',
                ]
        )
        runCommand("curl -L https://github.com/krallin/tini/releases/download/v0.19.0/tini --output ./tini")
        runCommand("chmod +x ./tini")
        
        copyFile("${jar.archiveName}", "/")
        exposePort(8080)
        entryPoint("/home/jboss/tini", "--", "java", "-jar", "/${jar.archiveName}")
    }

    task buildDockerImage(type: DockerBuildImage) {
        dependsOn createDockerfile
        dependsOn build
        inputDir = createDockerfile.destFile.get().asFile.parentFile
        doFirst {
            copy {
                from jar
                into inputDir
            }
        }
        def repo = [
                "rest-resource-executable" : "core",
                "migration-executable"     : "migration"
        ][project.name]

        images.add("$registry$repo:$project.version")

        def v = versionDetails()
        if (v.gitHash) {
            images.add("$registry$repo:${v.gitHash}")
        }
        if (v.branchName) {
            images.add("$registry$repo:${v.branchName.replaceAll("/", "-")}")
        }
    }

    task removeDockerImage(type: DockerRemoveImage) {
        force = true
        targetImageId buildDockerImage.getImageId()
    }

    task pushDockerImage(type: DockerPushImage) {
        dependsOn buildDockerImage
        images = buildDockerImage.images.get()
    }

    task pushDockerTags {
        dependsOn tasks.withType(DockerPushImage)
    }

    if (project.hasProperty("removeImage")) {
        pushDockerTags.finalizedBy removeDockerImage
    }
}