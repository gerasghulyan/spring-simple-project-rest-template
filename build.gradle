buildscript {
    ext {
        platformGroup = 'com.vntana.core'
        platformVersion = '0.1.0'
        kotlinVersion = '1.3.61'
        vavrVersion = '0.10.2'
        easymockVersion = '4.0.2'
        apacheCommonsValidatorVersion = '1.4.1'
        testContainersVersion = '1.11.3'
        orikaSpringBootVersion = "1.8.0"
        springFoxSwaggerVersion = '2.9.2'
        sflNotifierVersion = '1.7.0'
        commonsApiModelsVersion = '0.1.2'
        vntanaCacheVersion = '0.1.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://nexus.vntana.com/repository/maven-public'
            credentials {
                username System.env.SONATYPE_USERNAME
                password System.env.SONATYPE_PASSWORD
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.8.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "org.jetbrains.kotlin.plugin.spring" version "1.3.50"
    id "org.ajoberstar.grgit" version "3.0.0"
    id 'com.bmuschko.docker-remote-api' version "4.2.0"
    id 'com.palantir.git-version' version '0.11.0'
    id "org.sonarqube" version "2.7.1"
}

def environmentPlatformVersion() {
    switch (grgit.getBranch().current().name) {
        case "develop":
            return "$platformVersion-SNAPSHOT"
        case "release":
            return "$platformVersion-release-SNAPSHOT"
        case "master":
            return platformVersion
        default:
            return "$platformVersion-SNAPSHOT"
    }
}

def versionPostfix() {
    switch (grgit.getBranch().current().name) {
        case "develop":
            return "-SNAPSHOT"
        case "release":
            return "-release-SNAPSHOT"
        case "master":
            return ""
        default:
            return "-SNAPSHOT"
    }
}

task printEnvVersion() {
    doLast {
        println version
    }
}

task tagRelease() {
    description = 'Tags the current head with the project\'s version.'
    doLast {
        grgit.tag.add {
            name = version
            message = "Release of ${version}"
        }
        grgit.push()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "org.jetbrains.kotlin.jvm"
    apply plugin: "io.spring.dependency-management"

    group = platformGroup
    version = environmentPlatformVersion()
    sourceCompatibility = '1.8'

    task makePretty(type: Delete) {
        delete 'out'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url 'https://nexus.vntana.com/repository/maven-public'
            credentials {
                username System.env.SONATYPE_USERNAME
                password System.env.SONATYPE_PASSWORD
            }
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        implementation 'org.apache.commons:commons-lang3'
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "io.vavr:vavr"
        testImplementation "org.easymock:easymock"
        testImplementation "junit:junit"
        testImplementation "org.assertj:assertj-core"
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.1.8.RELEASE") {
                bomProperty("kotlin.version", kotlinVersion)
            }
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
        }
        dependencies {
            dependency("org.easymock:easymock:$easymockVersion")
            dependency("org.easymock:easymock:$easymockVersion")
            dependency("io.vavr:vavr:$vavrVersion")
            dependency("commons-validator:commons-validator:$apacheCommonsValidatorVersion")
            dependency("org.testcontainers:postgresql:$testContainersVersion")
            dependency("com.sflpro.notifier:api-client-library:$sflNotifierVersion")
            dependency("net.rakugakibox.spring.boot:orika-spring-boot-starter:${orikaSpringBootVersion}")
            dependency("io.springfox:springfox-swagger2:$springFoxSwaggerVersion")
            dependency("io.springfox:springfox-swagger-ui:$springFoxSwaggerVersion")
            dependency("com.vntana.commons:api-models:$commonsApiModelsVersion${versionPostfix()}")
            dependency("com.vntana.commons:cache:$commonsApiModelsVersion${versionPostfix()}")
            dependency("com.vntana.cache:vntana-cache:$vntanaCacheVersion${versionPostfix()}")
        }
    }
}

project(":domain") {

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
    }
}

project(":persistence:persistence-core") {

    dependencies {
        implementation project(":domain")
    }
}

project(":persistence:persistence-impl") {

    dependencies {
        implementation project(":domain")
        implementation project(":persistence:persistence-core")
    }
}

project(":commons:persistence-utils") {

    group = "${platformGroup}.persistence"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    }

}

project(":migration:migration-library") {

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.flywaydb:flyway-core'
        runtimeOnly 'org.postgresql:postgresql'
    }

}

project(":migration:migration-executable") {

    group = "${platformGroup}.migration"

    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "${group}.boot.MigrationBootApplication"
        manifest {
            attributes(
                    "Implementation-Version": environmentPlatformVersion()
            )
        }
    }
    dependencies {
        implementation project(":migration:migration-library")
        implementation "org.springframework.boot:spring-boot-starter"
    }
}

project(":notification") {

    dependencies {
        implementation "com.sflpro.notifier:api-client-library"
        implementation 'org.springframework:spring-context'
        implementation 'org.glassfish.jersey.inject:jersey-hk2'
        implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider'
        testImplementation "org.easymock:easymock"
    }
}

project(":helper:test-helper") {

    dependencies {
        implementation project(":domain")
        implementation project(":service:service-core")
        implementation project(":api:rest:rest-model")
        implementation "org.assertj:assertj-core"
        implementation 'org.springframework:spring-context'
    }
}

project(":helper:rest-test-helper") {

    group = "${platformGroup}.helper"
    apply plugin: "kotlin-spring"

    dependencies {
        implementation project(":domain")
        api project(":helper:test-helper")
        implementation project(":api:rest:rest-model")
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework:spring-context'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
        api "org.assertj:assertj-core"
    }
}

project(":helper:rest-resource-test-helper") {

    group = "${platformGroup}.helper"
    apply plugin: "kotlin-spring"

    dependencies {
        api project(":helper:rest-test-helper")
        implementation project(":api:rest:rest-client")
        implementation project(":api:rest:rest-model")
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework:spring-context'
        implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    }
}

project(":service:service-core") {

    group = "${platformGroup}.service"

    dependencies {
        implementation project(":domain")
    }
}

project(":service:service-impl") {

    apply plugin: "kotlin-spring"

    group = "${platformGroup}.service"

    dependencies {
        implementation project(":domain")
        implementation project(":persistence:persistence-core")
        implementation project(":persistence:persistence-impl")
        implementation project(":commons:persistence-utils")
        implementation project(":service:service-core")
        implementation "org.springframework.security:spring-security-crypto"
        implementation project(":listener:listener-core")
        testImplementation project(":helper::test-helper")
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.easymock:easymock"
    }
}

project(":service:service-integration-test") {

    group = "${platformGroup}.service"

    apply plugin: "kotlin-spring"

    dependencies {
        testImplementation project(":helper:test-helper")
        testImplementation project(":domain")
        testImplementation project(":service:service-core")
        implementation project(":migration:migration-library")
        runtimeOnly project(":service:service-impl")
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation "org.springframework.security:spring-security-crypto"
        testImplementation "org.testcontainers:postgresql"
        testImplementation "org.postgresql:postgresql"
    }
}

project(":api:rest:rest-model") {

    apply plugin: 'maven'

    archivesBaseName = 'rest-model-core'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.vntana.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.vntana.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.version = environmentPlatformVersion()
                pom.artifactId = "rest-model-core"
                pom.groupId = platformGroup
            }
        }
    }

    dependencies {
        api "com.fasterxml.jackson.core:jackson-annotations"
        compile "com.vntana.commons:api-models"
        implementation "commons-validator:commons-validator"
    }
}

project(":api:rest:rest-client") {

    apply plugin: 'maven'

    archivesBaseName = 'rest-client-core'

    uploadArchives {
        repositories {
            mavenDeployer {

                repository(url: "https://nexus.vntana.com/repository/maven-releases/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://nexus.vntana.com/repository/maven-snapshots/") {
                    authentication(userName: System.env.SONATYPE_USERNAME, password: System.env.SONATYPE_PASSWORD)
                }

                pom.version = environmentPlatformVersion()
                pom.artifactId = "rest-client-core"
                pom.groupId = platformGroup
            }
        }
    }

    dependencies {
        compile project(":api:rest:rest-model")
        compile "org.springframework.cloud:spring-cloud-starter-openfeign"
    }
}

project(":api:rest:rest-facade") {

    group = "${platformGroup}.rest.facade"

    dependencies {
        implementation project(":api:rest:rest-model")
        implementation project(":service:service-core")
        implementation project(":notification")
        implementation project(":commons:persistence-utils")
        runtimeOnly project(":service:service-impl")
        implementation project(":domain")
        implementation "net.rakugakibox.spring.boot:orika-spring-boot-starter"
        implementation "org.springframework:spring-context"
        implementation "com.sflpro.notifier:api-client-library"
        testImplementation project(":helper:test-helper")
        testImplementation project(":helper:rest-test-helper")
        testImplementation "org.easymock:easymock"
    }
}

project(":api:rest:rest-resource") {

    group = "${platformGroup}.rest.resource"

    dependencies {
        api project(":api:rest:rest-facade")
        api project(":api:rest:rest-model")
        api "org.springframework.boot:spring-boot-starter-web"
        api "org.aspectj:aspectjrt"
        api "org.aspectj:aspectjweaver"
        runtimeOnly project(":listener:listener-impl")
    }
}

project(":api:rest:rest-resource-executable") {

    group = "${platformGroup}.rest.resource"

    apply plugin: "org.springframework.boot"

    bootJar {
        mainClassName = "${platformGroup}.rest.resource.boot.VntanaSpringBootWebApplication"
        manifest {
            attributes(
                    "Implementation-Version": environmentPlatformVersion()
            )
        }
    }

    dependencies {
        implementation project(":api:rest:rest-resource")
        implementation 'org.springframework.data:spring-data-jpa'
        implementation "io.springfox:springfox-swagger2"
        implementation "io.springfox:springfox-swagger-ui"
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
        implementation "org.postgresql:postgresql"
        implementation "com.sflpro.notifier:api-client-library"
        implementation "com.vntana.cache:vntana-cache"
        implementation "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider"
    }
}

project(":api:rest:rest-resource-integration-test") {

    apply plugin: "kotlin-spring"

    dependencies {
        implementation project(":api:rest:rest-client")
        implementation "org.springframework.boot:spring-boot-starter"
        testImplementation project(":api:rest:rest-resource")
        testImplementation project(":migration:migration-library")
        testImplementation project(":helper:rest-resource-test-helper")
        testImplementation "com.vntana.cache:vntana-cache"
        testImplementation "com.sflpro.notifier:api-client-library"
        testImplementation "org.springframework.boot:spring-boot-starter-test"
        testImplementation 'org.springframework.data:spring-data-jpa'
        testImplementation "org.testcontainers:postgresql"
        testImplementation "org.postgresql:postgresql"
        testImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        testImplementation 'com.fasterxml.jackson.module:jackson-module-parameter-names'
        testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'
        testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    }
}

project(":listener:listener-core") {

    dependencies {
        api project(":domain")
    }
}

project(":listener:listener-impl") {

    dependencies {
        api project(":listener:listener-core")
        api "com.vntana.cache:vntana-cache"
        testImplementation project(":helper:test-helper")
    }
}

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage
import com.bmuschko.gradle.docker.tasks.image.DockerRemoveImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

configure(subprojects.findAll {
    it.name == "rest-resource-executable" || it.name == "scheduler" || it.name == "queue-consumer" || it.name == "migration-executable"
}) {

    apply plugin: 'com.bmuschko.docker-remote-api'

    def registry = project.findProperty("dockerRegistry") ?: ""
    if (registry) {
        registry += "/"
    }
    docker {
        registryCredentials {
            url = "https://$registry"
            username = System.getenv("DOCKER_REGISTRY_USERNAME")
            password = System.getenv("DOCKER_REGISTRY_PASSWORD")
        }
    }

    task createDockerfile(type: Dockerfile) {
        destFile = project.layout.buildDirectory.file('docker/Dockerfile')
        from 'openjdk:8-jre-alpine'
        label(
                [
                        'maintainer': 'vntana@sfl.am',
                ]
        )
        copyFile("${jar.archiveName}", "/")
        exposePort(8080)
        entryPoint("java", "-jar", "/${jar.archiveName}")
    }

    task buildDockerImage(type: DockerBuildImage) {
        dependsOn createDockerfile
        dependsOn build
        inputDir = createDockerfile.destFile.get().asFile.parentFile
        doFirst {
            copy {
                from jar
                into inputDir
            }
        }
        def repo = [
                "rest-resource-executable": "core",
                "migration-executable"    : "migration"
        ][project.name]
        tags.add("$registry$repo:$project.version")
        def v = versionDetails()
        if (v.gitHash) {
            tags.add("$registry$repo:${v.gitHash}")
        }
        if (v.branchName) {
            tags.add("$registry$repo:${v.branchName.replaceAll("/", "-")}")
        }
    }

    task removeDockerImage(type: DockerRemoveImage) {
        force = true
        targetImageId buildDockerImage.getImageId()
    }

    buildDockerImage.tags.get().each { tag ->
        tasks.create(name: "pushDockerTag_${tag.replaceAll('[/\\\\:<>"\\?\\*\\|]', "-")}", type: DockerPushImage) {
            dependsOn buildDockerImage
            imageName = tag
        }
    }

    task pushDockerTags {
        dependsOn tasks.withType(DockerPushImage)
    }

    if (project.hasProperty("removeImage")) {
        pushDockerTags.finalizedBy removeDockerImage
    }
}